---
title: "BEE 4750 Homework 4: Generating Capacity Expansion"
format:
    html:        
        warning: true
        error: true
    pdf:
        warning: true
        error: true
        code-annotation: below
jupyter: julia-1.9
format-links: []
---

::: {.content-visible when-format="ipynb"}
**Name**:

**ID**:
:::

::: {.callout-important icon=false}
### Due Date

Friday, 10/27/23, 9:00pm
:::

::: {.content-visible when-format="html"}

:::{.callout-caution}

If you are enrolled in the course, make sure that you use the GitHub Classroom link provided in Ed Discussion, or you may not be able to get help if you run into problems.

Otherwise, you can [find the Github repository here]({{< var github_org.repo >}}/hw04).

:::

:::

## Overview

### Instructions

- In Problem 1, you will formulate, solve, and analyze a standard generating capacity expansion problem.
- In Problem 2, you will add a CO~2~ constraint to the capacity expansion problem and identify changes in the resulting solution.

### Load Environment

The following code loads the environment and makes sure all needed packages are installed. This should be at the start of most Julia scripts.

```{julia}
#| output: false
import Pkg
Pkg.activate(@__DIR__)
Pkg.instantiate()
```

```{julia}
using JuMP
using HiGHS
using DataFrames
using Plots
using Measures
using CSV
using MarkdownTables
```

## Problems (Total: 100 Points)

For this problem, we will use hourly load (demand) data from 2013 in New York's Zone C (which includes Ithaca). The load data is loaded and plotted below.

```{julia}
#| echo: true
#| output: true
#| label: fig-demand
#| fig-cap: Hourly load data for New York's Zone C in 2013.

# load the data, pull Zone C, and reformat the DataFrame
NY_demand = DataFrame(CSV.File("data/2013_hourly_load_NY.csv"))
rename!(NY_demand, :"Time Stamp" => :Date)
demand = NY_demand[:, [:Date, :C]]
rename!(demand, :C => :Demand)
demand[:, :Hour] = 1:nrow(demand)

# plot demand
plot(demand.Hour, demand.Demand, xlabel="Hour of Year", ylabel="Demand (MWh)", label=:false)
```

Next, we load the generator data. This data includes fixed costs (\$/MW installed), variable costs (\$/MWh generated), and CO~2~ emissions intensity (tCO~2~/MWh generated).

```{julia}
#| echo: true
#| label: tbl-generators
#| tbl-cap: Generator type data, including fixed costs ($/MW installed), variable costs ($/MWh generated), and CO~2~ emissions intensity (tCO~2~/MWh generated).

gens = DataFrame(CSV.File("data/generators.csv"))
```

Finally, we load the hourly solar and wind capacity factors, which are plotted in @fig-cf. These tell us the fraction of installed capacity which is expected to be available in a given hour for generation (typically based on the average meteorology).

```{julia}
#| echo: true
#| label: fig-cf
#| layout-nrow: 2
#| fig-cap: "Hourly solar and wind capacity factors."
#| fig-subcap:
#|   - "January"
#|   - "July"

# load capacify factors into a DataFrame
cap_factor = DataFrame(CSV.File("data/wind_solar_capacity_factors.csv"))

# plot January capacity factors
p1 = plot(cap_factor.Wind[1:(24*31)], label="Wind")
plot!(cap_factor.Solar[1:(24*31)], label="Solar")
xaxis!("Hour of the Month")
yaxis!("Capacity Factor")

p2 = plot(cap_factor.Wind[4344:4344+(24*31)], label="Wind")
plot!(cap_factor.Solar[4344:4344+(24*31)], label="Solar")
xaxis!("Hour of the Month")
yaxis!("Capacity Factor")

display(p1)
display(p2)
```

You have been asked to develop a generating capacity expansion plan for the utility in Riley, NY, which currently has no existing electrical generation infrastructure. The utility can build any of the following plant types: geothermal, coal, natural gas combined cycle gas turbine (CCGT), natural gas combustion turbine (CT), solar, and wind. 

While coal, CCGT, and CT plants can generate at their full installed capacity, geothermal plants operate at maximum 85% capacity, and solar and wind available capacities vary by the hour depend on the expected meteorology. The utility will also penalize any non-served demand at a rate of $1000/MWh.  

::: {.callout-tip}
## Significant Digits

Use `round(x; digits=n)` to report values to the appropriate precision!
:::

::: {.callout-tip}
## Getting Variable Output Values

`value.(x)` will report the values of a `JuMP` variable `x`, but it will return a special container which holds other information about `x` that is useful for `JuMP`. This means that you can't use this output directly for further calculations. To just extract the values, use `value.(x).data`.
:::


::: {.cell .markdown}
### Problem 1 (22 points)

Your first task is to find a capacity expansion plan which minimizes total costs of investment and operation.
:::

::: {.cell .markdown}
#### Problem 1.1 (2 points)

Identify and define the decision variables for the problem. Make sure to include units.

:::

***Solution***:

We need the following notation:

- $x_g$: built capacity for generator $g$ in MW;
- $y_{g,t}$: generated power from generator $g$ in time period $t$ in MWh;
- $NSE_t$: unserved energy in time $t$.

::: {.cell .markdown}
#### Problem 1.2 (3 points)

Formulate the objective function. Make sure to include any needed derivations or justifications for your equation(s) and define any additional required notation beyond that introduced in Problem 1.1.

:::

***Solution***:

The objective is to minimize total costs ($C$) of expansion and generation, which we can split into fixed costs ($\text{FixedCost}$, \$/MW installed), variable costs of generation (\$/MWh generated), and the non-served energy penalty (\$/MWh unserved).

We can write this as
$$\min_{x_g, y_{g,t}} \sum_{g \in \mathcal{G}} \text{FixedCost}_g \times x_g + \sum_{t \in \mathcal{T}} \text{VarCost}_g \times y_{g,t} + \sum_{t \in \mathcal{T}} \text{NSECost}_t \times NSE_t,$$
where $\mathcal{G}$ is the set of generators and $\mathcal{T}$ is the set of time periods.

::: {.cell .markdown}
#### Problem 1.3 (4 points)

Derive all relevant constraints. Make sure to include any needed justifications or derivations.
:::

***Solution***:

The first constraint is non-negativity; all decision variables must be greater than or equal to zero, so $x_g \geq 0$, $y_{g,t} \geq 0$, and $NSE_t \geq 0$.

Second, the amount of generated power is limited by the capacity of that plant type. This is slightly more complicated because we need to account for the variable capacity factors of the wind and solar plants. We can summarize this with $$y_{g,t} \leq x_g \times cf_{g,t}$$ for all $t \in \mathcal{T}$, where $cf_{g,t}$ is the capacity factor for plant type $g$ in time $t$. We can either just assume that $cf_{g,t} = 1$ for non-variable plants $g$ (other than geothermal, where $cf = 0.85$) or we can split this into multiple constraints.

Third, the sum of generated power and un-served energy must be equal to the demand in that time period: $$\sum_{g \in G} y_{g,t} + NSE_t = d_t$$ for all $t \in \mathcal{T}$, where $d_t$ is the demand in MWh.


::: {.cell .markdown}
#### Problem 1.4 (3 points)

Implement your optimization problem in `JuMP`.

:::

***Solution***:

As mentioned above, we can either split the capacity constraint into multiple statements, or we can set up a single capacity factor matrix which combines the variable wind and solar capacity factors and constant values of 1 for the other plant types. We will do the second in this solution, but the first would get you the same answers (you would just have one-two more constraints defined, depending on the specifics).

```{julia}
# define sets
G = 1:nrow(gens)
T = 1:nrow(demand)

# set up auxiliary parameters
NSECost = 1000 # cost of unserved energy
# capacity factor matrix
cf_constant = ones(T[end], G[end-2]) # last two plants are wind and solar, so drop those
cf_constant[:, 1] .= 0.85
cf = hcat(cf_constant, cap_factor[!, :Wind], cap_factor[!, :Solar]) # append wind and solar capacity factors


# set up model object
gencap = Model(HiGHS.Optimizer) # use the HiGHS LP solver

# define variables
@variable(gencap, x[g in G] >= 0) # installed capacity
@variable(gencap, y[g in G, t in T] >= 0) # generated power
@variable(gencap, nse[t in T] >= 0) # unserved energy

# define objective: minimize sum of fixed costs, variable costs of generation, 
# and non-served energy penalty
@objective(gencap, Min, sum(gens[:, :FixedCost] .* x) + 
    sum(gens[:, :VarCost] .* [sum(y[g, :]) for g in G]) + 
    NSECost * sum(nse))

# define constraints
@constraint(gencap, capacity[g in G, t in T], y[g, t] <= x[g] * cf[t, g]) # capacity constraint
@constraint(gencap, demand_met[t in T], sum(y[:, t]) + nse[t] == demand.Demand[t]) # demand constraint
```

::: {.cell .markdown}

#### Problem 1.5 (5 points)

Find the optimal solution. How much should the utility build of each type of generating plant? What will the total cost be? How much energy will be non-served?
:::

***Solution***:

First, let's find the solution.

```{julia}
optimize!(gencap);
```

We can find how much generating capacity we want to build for each plant type by querying the relevant decision variable `x`. We will turn this into a DataFrame to make the presentation easier.

```{julia}
built_cap = value.(x).data
DataFrame(Plant=gens.Plant, Capacity=round.(built_cap; digits=0))
```

Similarly, we can find the total amount of un-served energy by adding up `nse[t]`.

```{julia}
sum(value.(nse).data)
```

This plan results in 6000 MWh of un-served energy throughout the year.

Finally, to get the total cost of the system we can use `objective_value`:

```{julia}
objective_value(gencap)
```

The total cost of this plan is $\$6.4 \times 10^8$.

::: {.cell .markdown}

#### Problem 1.6 (5 points)

What fraction of annual generation does each plant type produce? How does this compare to the breakdown of built capacity that you found in Problem 1.5? Do these results make sense given the generator data?

:::

***Solution***:

To find the total annual generation from each plant, we want to sum up the values of the variable `y` along the time dimension.

```{julia}
annual_gen = [sum(value.(y[g, :]).data) for g in G]
```

We can then convert this into fractions of total generation, which we can compare to fractions of built capacity.

```{julia}
annual_gen_frac = annual_gen ./ sum(annual_gen)
built_frac = built_cap ./ sum(built_cap)
DataFrame(Plant=gens[!, :Plant], Built=built_frac, Generated=annual_gen_frac)
```

One observation is that that we have to overbuild the fraction of combustion turbine gas plants (NG CT) as these are needed when wind and solar is low, but otherwise are less commonly used. We also have to slightly overbuild wind and solar relative to the power generated by these technologies, as although they are free to generate, they can be severely constrained in terms of capacity in a given hour.

::: {.cell .markdown}
### Problem 2 (18 points)

The NY state legislature is considering enacting an annual CO~2~ limit, which for the utility would limit the emissions in its footprint to 1.5 MtCO~2~/yr.

:::

::: {.cell .markdown}
#### Problem 2.1 (3 points)

What changes are needed to your linear program from Problem 1? Re-formulate any part of the problem that has changed.

:::

***Solution***:

The only change is to add in a constraint for the CO~2~ limit. Let $emis_g$ be the CO~2~ emissions factor (tCO~2~/MWh generated) for plant $g$. Then this constraint is:
$$\sum_{g \in G} emis_g \times \sum_{t \in T} y_{g,t} \leq 1500000.$$

::: {.cell .markdown}
#### Problem 2.2 (3 points)

Implement the new optimization problem in `JuMP`. 

:::

***Solution***:

We can actually just add a new constraint to the `JuMP` model (just be careful when evaluating the notebook cells!), but if you re-formulated the model object with a new name, that works as well.

```{julia}
# add in the new constraint
@constraint(gencap, co2, sum(gens[:, :Emissions] .* [sum(y[g, :]) for g in G]) <= 1500000);
```

::: {.cell .markdown}
#### Problem 2.3 (5 points)

Find the optimal solution. How much should the utility build of each type of generating plant? What is different from your plan from Problem 1? Do these changes make sense?

:::

***Solution***:

Find the new solution:

```{julia}
optimize!(gencap)
built_cap_co2 = value.(x).data
DataFrame(Plant=gens.Plant, Capacity=round.(built_cap_co2; digits=0))
```

To meet the emissions constraint, we build a reduced amount of natural gas, which creates some interesting changes in the rest of the mix. This natural gas capacity is replaced by a combination of 271 MW of geothermal (which was previously zero), an additional 2000 MW of wind capacity, and slightly more solar. These massive increases in wind are required to ensure adequate generation when solar is low, when otherwise we could rely on gas for that purpose.

```{julia}
sum(value.(nse).data)
```

We also increase the amount of non-served energy from Problem 1 by about 50% due to the reduced amount of built natural gas capacity.

::: {.cell .markdown}
#### Problem 2.4 (5 points)

What fraction of annual generation does each plant type produce? How does this compare to the breakdown of built capacity that you found in Problem 2.3? What are the differences between these results and your plan from Problem 1?
:::

***Solution***:

We can just repeat the process from Problem 1.6.

```{julia}
annual_gen_co2 = [sum(value.(y[g, :]).data) for g in G]
annual_gen_frac_co2 = annual_gen_co2 ./ sum(annual_gen_co2)
built_frac_co2 = built_cap_co2 ./ sum(built_cap_co2)
DataFrame(Plant=gens[!, :Plant], Built_CO2=built_frac_co2, Generated_CO2=annual_gen_frac_co2, Built_Old=built_frac, Generated_Old=annual_gen_frac)

```

The impact of the CO~2~ constraint is similar to Problem 2.3: we rely on geothermal and wind heavily, so the total generation percentages is higher than previously (and for geothermal, higher than its built capacity percentage). Meanwhile, NG CT generation is limited to emergency shortfalls, and it represents only 1% of total generation. We also use NG CCGT a lot less, even though its built capacity isn't much lower in absolute terms.

::: {.cell .markdown}
#### Problem 2.5 (2 points)

What would the value to the utility be of allowing it to emit an additional 1000 tCO~2~/yr? An additional 5000?

:::

***Solution***:

To answer this, we want to find the shadow price of the CO~2~ emissions constraint. 

```{julia}
shadow_price(co2)
```

Thus, every extra tCO~2~/yr allowed would reduce the cost of generation by \$183. So an extra 1000 tCO~2~/yr would correspond to a value of \$183,000, since the objective and constraints are linear. And an extra 5000 tCO~2~/yr would be worth \$915,000.

::: {.cell .markdown}
## References

List any external references consulted, including classmates.
:::